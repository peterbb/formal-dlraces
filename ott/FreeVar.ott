embed
{{coq
Require Export MSetPositive MSetProperties.
Module PS := PositiveSet.
Module PSProp := WPropertiesOn PS.
}}

grammar
free_ls_var :: '' ::= {{coq PS.t}}
    | FV( D )       :: M:: FV_ls_D {{coq (free_ls_D [[D]])}}
    | FV( G )       :: M:: FV_ls_G {{coq (free_ls_G [[G]])}}
    | FV( C )       :: M:: FV_ls_C {{coq (free_ls_C [[C]])}}

free_le_var :: '' ::= {{coq PS.t}}
    | FV( D )       :: M:: FV_le_D {{coq (free_le_D [[D]])}}
    | FV( G )       :: M:: FV_le_G {{coq (free_le_G [[G]])}}
    | FV( C )       :: M:: FV_le_C {{coq (free_le_C [[C]])}}

free_formula :: '' ::=
    | rho1 .. rhok notIn free_ls_var :: M:: NotInLsFree
                                           {{coq (Forall 
                                              (fun x => ~PositiveSet.In x [[free_ls_var]])
                                              [[rho1 .. rhok]])}}
    | X1 .. Xk notIn free_le_var     :: M:: NotInLeFree
                                           {{coq (Forall 
                                              (fun x => ~PositiveSet.In x [[free_le_var]])
                                              [[X1 .. Xk]])}}

embed
{{coq
Require Import Syntax TypeSyntax.

Section Traverse.
    (* Functions which walk the syntax tree, looking for free
     * variables.
     *)

    Variable remember_rho : ls_var -> PS.t.
    Variable remember_X : le_var -> PS.t.
    Variable select_binding : 
        list ls_var -> list le_var -> list positive.

    Definition list_to_set (xs : list positive) : PS.t :=
        fold_right (fun x set => PS.add x set) PS.empty xs.

    Fixpoint traverse_r (r : lockset) : PS.t :=
        match r with
        | ls_Var rho => remember_rho rho
        | ls_Single _ => PS.empty
        | ls_Union r1 r2 =>
            PS.union (traverse_r r1) (traverse_r r2)
        end.

    Fixpoint traverse_D (D : lockenv) : PS.t :=
        match D with
        | le_Empty => PS.empty
        | le_Var X => remember_X X
        | le_Append D' r _ => PS.union (traverse_D D') (traverse_r r)
        | le_Add D1 D2 => PS.union (traverse_D D1) (traverse_D D2)
        | le_Sub D1 D2 => PS.union (traverse_D D1) (traverse_D D2)
        end.

    Fixpoint traverse_AT (AT : annotatedtype) : PS.t :=
        match AT with
        | at_Lock r => traverse_r r
        | at_Arrow AT1 (EffectIntro D1 D2) AT2 =>
            PS.union (traverse_AT AT1)
              (PS.union (traverse_D D1)
                (PS.union (traverse_D D2)
                          (traverse_AT AT2)))
        | _ => PS.empty
        end.

    Definition traverse_ineq (c : ineq) : PS.t :=
        match c with
        | c_LockSet r1 r2 => 
            PS.union (traverse_r r1) (traverse_r r2)
        | c_LockEnv D1 D2 => 
            PS.union (traverse_D D1) (traverse_D D2)
        end.

    Definition traverse_C (C : constraints) : PS.t :=
        fold_right PS.union PS.empty (map traverse_ineq C).

    Definition traverse_SC (SC : scheme) : PS.t :=
        match SC with
        | sc_Quant rhos Xs C AT =>
            PS.diff (PS.union (traverse_C C) (traverse_AT AT))
                    (list_to_set (select_binding rhos Xs))
        end.

    Fixpoint traverse_G (G : context) : PS.t :=
        match G with
        | G_Empty => PS.empty
        | G_Extend G _ SC =>
            PS.union (traverse_G G) (traverse_SC SC)
        end.
End Traverse.

Definition select_rhos (rhos : list ls_var) (Xs : list le_var) := rhos.
Definition select_Xs  (rhos : list ls_var) (Xs : list le_var) := Xs.
Definition ignore (_:positive) := PS.empty.

Definition free_ls_D := traverse_D PS.singleton ignore.
Definition free_ls_C := traverse_C PS.singleton ignore.
Definition free_ls_AT := traverse_AT PS.singleton ignore.
Definition free_ls_G := traverse_G PS.singleton ignore select_rhos.

Definition free_le_D := traverse_D ignore PS.singleton.
Definition free_le_C := traverse_C ignore PS.singleton.
Definition free_le_AT := traverse_AT ignore PS.singleton.
Definition free_le_G := traverse_G ignore PS.singleton select_Xs.



Definition sc_close_find_vars Gvars Cvars ATvars :=
    PS.elements (PS.diff (PS.union Cvars ATvars) Gvars).

Definition sc_close G C AT : scheme :=
    sc_Quant (sc_close_find_vars (free_ls_G G) (free_ls_C C) (free_ls_AT AT))
             (sc_close_find_vars (free_le_G G) (free_le_C C) (free_le_AT AT))
             C AT.

}}
